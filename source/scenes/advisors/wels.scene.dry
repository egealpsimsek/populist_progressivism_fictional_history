title: August von Begea
is-pinned-card: true
tags: advisor, centrist
card-image: img/portraits/Begea.jpg
view-if: wels_advisor = 1
new-page: true

= August von Begea

August von Begea is the founder and leader of The All-Casoducrian Congress. He is a veteran of the great war who achieved the rank of Lieutenant Colonel purely due to his tactical skill and acumen. Charismatic, young, idealistic and a fiery orator; he is everything the new movement needs to rise to prominence.

- @clear_agenda
- @party_discipline
- @emergency_fundraising
- @snap_election
- @root: Return to main

@clear_agenda
title: Clear the agenda
subtitle: Removes all cards from the hand.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: {!
Q.advisor_action_timer = 6;
if (Q.difficulty >= 0) {
    window.dendryUI.dendryEngine.state.currentHands['main'] = [];
} else {
    window.dendryUI.dendryEngine.state.currentHands['main.main_easy'] = [];
}
!}

All items from our agenda have been cleared.

@party_discipline
title: Party Discipline
subtitle: Reduce dissent across the party.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; left_dissent -= 5; labor_dissent -= 10; center_dissent -= 10; reformist_dissent -= 10; neorevisionist_dissent -= 5

We have enforced party discipline to reduce dissent across all factions.

@emergency_fundraising
title: Emergency Fundraising
view-if: difficulty < 1
subtitle: An emergency infusion of resources.
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; resources += dues

We have gained +[+ dues +] resources.

@snap_election
title: Snap Election
subtitle: Call a snap election.
unavailable-subtitle: [? if prime_minister != "Begea" and president != "Begea" : Begea must be Prime Minister or President. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if president = "Begea" and presidential_powers : We have removed this power from the president with a constitutional reform. ?]
choose-if: advisor_action_timer == 0 and (prime_minister == "Begea" or (president == "Begea" and not presidential_powers))
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

We have called for new elections, set for [+ next_election_month : month +] [+ next_election_year +].
